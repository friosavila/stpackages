clear
set obs 1000
gen r=runiformint(1,3)
gen rr=rnormal()+r
sum rr
gen over = runiformint(1,3) 
capture program drop mhistogram
program mhistogram
	syntax varname [iw fw / ] [if] [in], [over(varname) width(real 0) ///
					start(string asis) * ///
					height(real 1.0) ///
					gap(real 0.0) cum V1(str asis)]
	
	tempname hist
	marksample touse
	markout `touse' `over' `exp'
	
	frame put `varlist' `exp' `over' if `touse', into(`hist')
	
	frame `hist':{
		
		sum `varlist' [`weight'`exp'], meanonly 
		local rmin = r(min)
		local rmax = r(max)
		local rn   = r(N)
		local k    = min(sqrt(`rn'), 10*ln(`rn')/ln(10))
		local bw   = (`rmax'-`rmin')/`k'
		local sw   = r(sum_w)
		if `width'==0 local width `bw'
		if "`start'"=="" local start `rmin'
		
		levelsof `over', local(oname)
		local wrdn: word count `oname' 
		
		forvalues ii = 1/`wrdn' {
			local i:word `ii' of `oname'
			local kc = `ii'
			twohistgen `varlist' [`weight'`exp'] if `over'==`i', `options' width(`width') start(`start') ///
						gen(h`kc' g`kc') 
			replace h`kc' = h`kc'*`height'+(`wrdn'-`kc')*`gap'
			local base`kc'=(`wrdn'-`kc')*`gap'
			
			if `gap'==0 & "`cum'"!="" {
				sum `varlist' [`weight'`exp'] if `over'==`i', meanonly 
				local lw = r(sum_w)/`sw'
				capture gen bsc = 0
				replace bsc = bsc + h`kc'*`lw'
				replace h`kc' = bsc 
				local tohist  (bar h`kc' g`kc' , pstyle(p`kc') barw(`width') base(`base`kc''))		`tohist'	
			}
			else {
				local tohist `tohist' (bar h`kc' g`kc' , pstyle(p`kc') barw(`width') base(`base`kc'') color(`color1'))			
			}	
		}	
		if `gap'!=0 local ysoff ylabel("")
		two `tohist', `ysoff'
	}
end

replace rr = rr + 3*over
mhistogram rr, over(over) gap(0.5)   
mhistogram rr, over(over) width(.3)  gap(0.5) 
